plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.home'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

apply from: "${project.projectDir}/libraries.gradle"

dependencies {
    implementation libpacks.springBase
    implementation libpacks.tools
    implementation libpacks.database
    implementation libpacks.swagger
    compileOnly libpacks.compileOnly
    annotationProcessor libpacks.annotationProcessor
    testImplementation libpacks.test.implementation
    testRuntimeOnly libpacks.test.runtimeOnly
}

publishing {
    repositories {
        maven {
            name = "Github"
            url = uri("https://maven.pkg.github.com/Sakerini/email-mfa")
            credentials {
                username = findProperty("github.username")
                password = findProperty("github.token")
            }
        }
    }
    publications {
        register("jar", MavenPublication) {
            from(components["java"])
            pom {
                url.set("https://github.com/Sakerini/email-mfa.git")
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
